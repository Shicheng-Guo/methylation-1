/* Tools:
 *   Bowtie2
 *   SAMtools
 *   Bismark
 *   SRAtools
 *   Sherman
 *   FastQC
 *   cutadapt
 *   trim galore
 *   r-base
 */

// TASK DEFINITION

// untar
deftask untar( <out( File )> : tar( File ) )in bash *{
  tar xf $tar
  out=`tar tf $tar`
}*

// cat
deftask cat( out( File ) : <file( File )> )in bash *{
  cat ${file[@]} > $out
}*

// fastq-dump
deftask fastq-dump( fastq1( File ) fastq2( File ) : sra( File ) )in bash *{

  fastq=$sra.fastq

  fastq-dump --split-3 $sra

  base=${sra##*/}
  base=${base::-4}
  fastq1=${base}_1.fastq
  fastq2=${base}_2.fastq
}*

// gunzip
deftask gunzip( out( File ) : gz( File ) )in bash *{
  out=${gz%.gz}
  gzip -c -d $gz > $out
}*

// fastqc
deftask fastqc( zip( File ) : fastq( File ) )in bash *{
  fastqc -f fastq --noextract -o ./ $fastq
  zip=`ls *.zip`
}*

// bismark    
deftask bismark-prepare( idx( File ) : fa( File ) )in bash *{
  mkdir ref
  (cd ref; ln -s ../$fa)
  bismark_genome_preparation --bowtie2 ref
  tar chf $idx ref
}*

deftask bismark-align( sam( File ) : idx( File ) fastq( File  ) )in bash *{
  tar xf $idx
  bismark --sam --bowtie2 -N 1 -L 20 --phred33-quals ref $fastq
  sam=${fastq}_bismark_bt2.sam
}*

deftask bismark-extract( bismark_cov( File ) bedgraph( File ) : sam( File ) )in bash *{
  bismark_methylation_extractor -s --comprehensive --bedGraph $sam
  bedgraph=`ls *.bedGraph.gz`
  bismark_cov=`ls *.bismark.cov.gz`
}*

// sherman
deftask sherman( fastq( File ): len nread cr e <fa( File )> )in bash *{
  mkdir ref
  (cd ref; for i in ${fa[@]}; do ln -s ../$i; done)
  Sherman -cr $cr -e $e -l $len -n $nread --genome_folder ref
  fastq=simulated.fastq
}*

deftask sample( <s> : lo hi n ) in r *{
  s = sample( lo:hi, n )
}*

deftask trim-galore( trimmed( File ) : fastq( File ) )in bash *{
  trim_galore --rrbs --illumina $fastq
  trimmed=`ls *_trimmed.*`
}*

// samtools
deftask samtools-view( bam( File ) : sam( File ) )in bash *{
  samtools view -bS $sam > $bam
}*

deftask samtools-sort( sortedbam( File ) : bam( File ) )in bash *{
  sortedbam=alignment-sorted.bam
  samtools sort -m 2G $bam alignment-sorted
}*

deftask samtools-index( bai( File ): bam( File ) )in bash *{
  bai=$bam.bai
  samtools index $bam
}*

deftask report( pdf( File ) : )in r *{

}*



// INPUT DATA

lonread  =   9000;
hinread  =  10000;
nsplit   =      1;
len      =     80;
cr       =     90;
e        = '0.01';

// fa-tar = 'hg19/hg19.tar';
fa-gz = 'hg19/chr22.fa.gz';


// WORKFLOW DEFINITION

// fa = untar( tar: fa-tar );
fa = gunzip( gz: fa-gz );

idx = bismark-prepare( fa: fa );

s = sample(
  lo: lonread,
  hi: hinread,
  n:  nsplit );

fastq = sherman(
  len:   len,
  nread: s,
  fa:    fa,
  cr:    cr,
  e:     e );

trimmed = trim-galore( fastq: fastq );


sam = bismark-align(
  idx:    idx,
  fastq: trimmed );
  
bam = samtools-sort( bam: samtools-view( sam: sam ) );
bai = samtools-index( bam: bam );

bismark-cov bedgraph = bismark-extract( sam: sam );

qc = fastqc( fastq: fastq );

// QUERY
bedgraph bismark-cov bam bai;
